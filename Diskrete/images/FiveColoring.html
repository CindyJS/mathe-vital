<!DOCTYPE html>
<html>
<head>
    <meta charset="UTF-8">
    
    <title>FiveColoring.cdy</title>
    <style type="text/css">
        * {
            margin: 0px;
            padding: 0px;
        }

        #CSConsole {
            background-color: #FAFAFA;
            border-top: 1px solid #333333;
            bottom: 0px;
            height: 200px;
            overflow-y: scroll;
            position: fixed;
            width: 100%;
        }
    </style>
    <link rel="stylesheet" href="https://cindyjs.org/dist/v0.8/CindyJS.css">
    <script type="text/javascript" src="https://cindyjs.org/dist/v0.8/Cindy.js"></script>
<script id="csinit" type="text/x-cindyscript">
screenlimit=-8;
screenlimitX=-20;
pointdist=0.4;
segdist=0.4;

//Buttonhandler
modi=[Text0,Text1,Text2];
initmodi():=(
  apply(modi,m,m:"old"=false);
  modus=1; 
);
initmodi();
buttons():=(
    l=select(modi,m,(m:"old")!=(m.pressed));
    if(l!=[],apply(modi--l,m,m.pressed=false));
    apply(modi,m,m:"old"=m.pressed);
    modus=select(1..length(modi),i,(modi_i).pressed);
    modus=if(modus==[],0,modus_1);
    err(modus);
);
//Ende Buttonhandler


//Mouseevents abfangen
cont(s,pos):=(
  aa=s.begin;
  bb=s.end;
  pro=(meet(perp(s,pos),s)).xy;
  area(aa,pos,pro)*area(bb,pos,pro)<0 & |pro,pos|<segdist;
);

closepoints(pos):=select(allpoints(),p,|p.xy,pos|<pointdist);
closesegs(pos):=select(allsegments(),s,cont(s,pos));

mousefilter():=(
   pos=mouse().xy;
   if(pos.y<screenlimit % pos.x < screenlimitX,out=["none"],
     cp=closepoints(pos);
     cs=closesegs(pos);
     out=["new",pos];
     if(length(cs)==1,out=["seg",(meet(perp(cs_1,pos),cs_1)).xy,cs_1]);
     if(length(cp)==1,out=["old",cp_1]);
   );
   out;
);
//Ende Mouseevents abfangen

stdLabels = ["A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z"];
stdLabels = concat(stdLabels, apply(1..9999, "P" + #));

newpointlabel():=(
   pl=1;
   pts=allpoints();
   while(select(pts,p,p.label==stdLabels_pl)!=[],pl=pl+1);
   [stdLabels_pl, stdLabels_pl];
);

newseglabel():=(
   sl=1;
   segs=allsegments();
   while(select(segs,s,s.label=="S"+sl)!=[],sl=sl+1);
   ["S"+sl, " "];
);




segbyname(name):=(
   lis=select(allsegments(),#.label==name);
   if(lis!=[],lis_1,"");
);


segadd(p1,p2):=(
  lab=newseglabel();
  create([lab_1],"Segment",[p1,p2]);
  s2=segbyname(lab_1); 
  s2.printlabel=lab_2;
  s2;
);





generatepoint(pp):=(
   lab=newpointlabel();
   p=createpoint(lab_1,pp_2);
   p.printlabel=lab_2;
   p.color=(1,1,1);
   p;
);



generatepointonseg(pp):=(
   p=generatepoint(pp);
   s=pp_3;
   aa=s.begin;
   bb=s.end;
   removeelement(s);
   segadd(aa,p);
   segadd(bb,p);
   p;
);





pointaddcolor=(1,1,1);
pointsnapcolor=(1,1,0);
pointtmp(pp):=(
  if(pp_1=="new", draw(pp_2,size->5,color->pointaddcolor));
  if(pp_1=="old", draw(pp_2,size->5,color->pointsnapcolor));
  if(pp_1=="seg", draw(pp_2,size->5,color->pointsnapcolor));
);

segtmp(pp):=(
  if(pp_1=="new", draw(pp_2,size->5,color->pointaddcolor);draw(tmppt,pp_2));
  if(pp_1=="old", draw(pp_2,size->5,color->pointsnapcolor);draw(tmppt,pp_2));
  if(pp_1=="seg", draw(pp_2,size->5,color->pointsnapcolor);draw(tmppt,pp_2));
);

remove(pp):=(
   if(pp_1=="old", removeelement(pp_2));
   if(pp_1=="seg", removeelement(pp_3));
);


pointadd(pp):=(
  pout="";
  if(pp_1=="new", pout=generatepoint(pp));
  if(pp_1=="old", pout=pp_2);
  if(pp_1=="seg", pout=generatepointonseg(pp));
  pout;
);





//Modi
moduspressed(pp):=(
   if(modus==1,pointtmp(pp));
   if(modus==2,tmppt=pointadd(pp);tmppos=tmppt.xy);
   if(modus==3,);
);




modusdrag(pp):=(
   if(modus==1,pointtmp(pp));
   if(modus==2,tmppt.xy=tmppos;segtmp(pp));
   if(modus==3,);
);


modusreleased(pp):=(
   if(modus==1,pointadd(pp));
   if(modus==2,pp2=pointadd(pp);segadd(pp2,tmppt));
   if(modus==3,remove(pp));
);



//End Modi

buttons();

pcount=0;
scount=0;













;colors = [(1,0,0), (0,1,0), (0,0,1), (1,1,0), (1,0,1)];
colorNames = [[(1,0,0),"ROT"],[(0,1,0),"GRÜN"],[(0,0,1),"BLAU"],[(1,1,0),"GELB"],[(1,0,1),"MAGENTA"]];

deg(v, G) := (
	createvar(ret);
	
	ret = 0;
	forall(G_2, if(contains(#, v), ret = ret + 1));

	removevar(ret);
);

subGraph(v, G) := (
	createvar(ret);
	createvar(tmp);

	tmp = [];
	forall(G_2, if(!contains(#, v), tmp = append(tmp, #)));
	ret = [remove(G_1, v), tmp];

	removevar(tmp);
	removevar(ret);
);

reachableSubGraph(v, G, colorlist) := (
	createvar(ret);
	createvar(tmpVold);
	createvar(tmpVnew);
	createvar(tmpE);

	tmpVold = [];
	tmpVnew = [v];
	tmpE = [];

	while(tmpVold != tmpVnew,
		tmpVold = tmpVnew;

		forall(tmpVold, vertex,
			forall(G_2, edge,
				if(contains(edge, vertex) & contains(colorlist, (remove(edge, vertex)_1).color),
					tmpVnew = append(tmpVnew, remove(edge, vertex)_1);
					tmpE = append(tmpE, edge);

					tmpVnew = sort(set(tmpVnew));
				);
			);
		);
	);

	ret = [tmpVnew, sort(set(tmpE))];

	removevar(tmpE);
	removevar(tmpVnew);
	removevar(tmpVold);
	removevar(ret);
);

subGraphMulti(V, G) := (
	createvar(ret);

	ret = [G_1, G_2];
	forall(V, ret = subGraph(#, ret));

	removevar(ret);
);

getNeighbours(v, G) := (
	createvar(ret);

	ret = [];
	forall(G_2, if(contains(#, v), ret = append(ret, (remove(#, v)_1))));

	removevar(ret);
);

getNeighboursCounterClockWise(v, G) := (
	createvar(ret);
	createvar(tmp);

	tmp = [];
	ret = getNeighBours(v, G);
	ret = sort(ret, arctan2((# - v).x, (# - v).y));
	
	removevar(tmp);
	removevar(ret);
);

highlightGraph(G) := (
	forall(Vertices, #.size = 5; #.color = (0.4, 0.4, 0.4));
	forall(L, #.size = 1; #.color = (0.5, 0.5, 0.5));

	forall(G_1, #.size = #.size + 4);
	forall(G_2, e,
		tmp = remove(alllines(), SliderLine);
		forall(tmp, l, if(incidences(l) == e, l.color = (1, 1, 1); l.size = l.size + 2));
	);
);

fiveColor(G) := (
	if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [], "Coloriere Knoten: " + G_1]));

	createvar(j);
	createvar(exit);

	if(length(G_1) <= 5,
		if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [], "Triviales (Sub-)Problem: |" + G_1 + "| < 6."]));
		repeat(length(G_1), ((G_1)_#).color = colors_#),

		// else -> (sub-)graph has more than five vertices
		j = 1;
		exit = 0;

		while((j <= length(G_1)) & (exit == 0),
			if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [], "Suche Knoten v mit deg(v) < 6:"]));

			if(deg(((G_1)_j), G) > 5 & !(j == length(G_1)),
				if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "deg(" + inspect(((G_1)_j), "name") + ") > 5"]));
				j = j + 1,
				
				// else -> vertex with degree lower or equal than five (there has to exist one)
				// or we give a try to color a vertex with degree more than five!
				if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "deg(" + inspect(((G_1)_j), "name") + ")" + if(deg(((G_1)_j), G) <= 5, " <= 5", " > 5 (! TRY-Modus !)") + " -> Coloriere Subgraph " + subGraph(((G_1)_j), G)_1 + ":"]));
				fiveColor(subGraph(((G_1)_j), G));
				if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "deg(" + inspect(((G_1)_j), "name") + ")" + if(deg(((G_1)_j), G) <= 5, " <= 5", " > 5 (! TRY-Modus !)") + " -> Subgraph " + subGraph(((G_1)_j), G)_1 + "coloriert."]));

				neighbours = getNeighboursCounterClockWise(((G_1)_j), G);
				neighbourColors = apply(neighbours, #.color);

				if(deg(((G_1)_j), G) <= 4 % length(set(neighbourColors)) < 5,
					((G_1)_j).color = remove(colors, set(neighbourColors))_1;

					exit = 1,

					// else -> five different colors
if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), append(neighbours, ((G_1)_j)), "Konflikt mit Nachbarknoten " + neighbours + "."]));

hydra1 = reachableSubGraph(neighbours_1, subGraph((G_1)_j, G), [neighbourColors_1, neighbourColors_3]);
hydra1B = reachableSubGraph(neighbours_3, subGraph((G_1)_j, G), [neighbourColors_1, neighbourColors_3]);

hydra2 = reachableSubGraph(neighbours_2, subGraph((G_1)_j, G), [neighbourColors_2, neighbourColors_4]);
hydra2B = reachableSubGraph(neighbours_4, subGraph((G_1)_j, G), [neighbourColors_2, neighbourColors_4]);

tmp1 = [set(concat(hydra1_1, hydra1B_1)), set(concat(hydra1_2, hydra1B_2))];
tmp2 = [set(concat(hydra2_1, hydra2B_1)), set(concat(hydra2_2, hydra2B_2))];
tmp3 = [set(concat(tmp1_1, tmp2_1)), set(concat(tmp1_2, tmp2_2))];

if(!invalidGraph, history = append(history, [tmp3, apply(Vertices, #.color), [], "Betrachte Subgraphen generiert aus " + inspect(neighbours_1, "name") + " und " + inspect(neighbours_3, "name") + "."]));

if(!contains(hydra1_1, neighbours_3),
	// umswitchen 1 3
	forall(hydra1_1, if(#.color == neighbourColors_1, #.color = neighbourColors_3, #.color = neighbourColors_1));

if(!invalidGraph, history = append(history, [tmp3, apply(Vertices, #.color), [], "Es existier kein " + apply(select(colorNames, #_1 == neighbourColors_1 % #_1 == neighbourColors_3), cn, cn = cn_2) + "-Weg von " + inspect(neighbours_1, "name") + " nach " + inspect(neighbours_3, "name") + " -> Colorswitch " + inspect(neighbours_1, "name") + "."]));

if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "deg(" + inspect(((G_1)_j), "name") + ")" + if(deg(((G_1)_j), G) <= 5, " <= 5", " > 5 (! TRY-Modus !)") + " -> Subgraph coloriert durch Konfliktlösung."]));
	((G_1)_j).color = neighbourColors_1;
	,
	// else umswitchen 1 4
	if(!invalidGraph, history = append(history, [tmp3, apply(Vertices, #.color), [], "Es existiert ein " + apply(select(colorNames, #_1 == neighbourColors_1 % #_1 == neighbourColors_3), cn, cn = cn_2) + "-Weg von " + inspect(neighbours_1, "name") + " nach " + inspect(neighbours_3, "name") + "."]));

	forall(hydra2_1, if(#.color == neighbourColors_2, #.color = neighbourColors_4, #.color = neighbourColors_2));

if(!invalidGraph, history = append(history, [tmp3, apply(Vertices, #.color), [], "Lösung -> Colorswitch " + inspect(neighbours_2, "name") + "."]));

if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "deg(" + inspect(((G_1)_j), "name") + ")" + if(deg(((G_1)_j), G) <= 5, " <= 5", " > 5 (! TRY-Modus !)") + " -> Subgraph coloriert durch Konfliktlösung."]));
	((G_1)_j).color = neighbourColors_2;
);	

					exit = 1;
				);
			);
		);
	);

	// Test auf invaliden Graphentyp:
	if(length(remove(colors, apply(getNeighboursCounterClockWise(((G_1)_j), G), #.color))) == 0,
		history = append(history, [G, apply(Vertices, #.color), [((G_1)_j)], "Invalider Graphentyp! Färbarkeit nur für planare Graphen garantiert."]);
		invalidGraph = true;
	);

	removevar(j);
	removevar(exit);

	if(!invalidGraph, history = append(history, [G, apply(Vertices, #.color), [], "Colorierung von " + G_1 + " fertig."]));
);

history = [];
Vertices = [];
Edges = [];
L = [];
invalidGraph = false;

init() := (
	invalidGraph = false;
	history = [];
	//Slider.xy = SliderBegin.xy;

	L = remove(alllines(), SliderLine);
	Vertices = sort(remove(allpoints(), [SliderBegin, Slider, SliderEnd]));
	Edges = remove(apply(L, incidences(#)), [[]]);  // Fix für [] Bug

	forall(Vertices, #.size = 6; #.color = (0,0,0));
	forall(L, #.size = 2; #.color = (1, 1, 1));

	fiveColor([Vertices, Edges]);
	forall(L, #.size = 2; #.color = (1, 1, 1));

	text();
	repaint();
);

text() := (
	state = floor(length(history)*(-Slider.y + SliderBegin.y)/(-SliderEnd.y + SliderBegin.y)) + 1;

	if(state == 0,
		,
		// else
		if(state > length(history), state = length(history));

		repeat(length(Vertices), (Vertices_#).size = 6);
		highlightGraph((history_state)_1);
		repeat(length(Vertices), (Vertices_#).color = ((history_state)_2)_#);
		
		if((history_state)_3 != [], forall((history_state)_3, #.size = #.size + 6));

		if(state - 2 >= 1, drawtext([Slider.x + 0.5, Slider.y + 1.4], (history_(state - 2))_4, color->(0.6, 0.6, 0.6)));
		if(state - 1 >= 1, drawtext([Slider.x + 0.5, Slider.y + 0.6], (history_(state - 1))_4, color->(0.5, 0.5, 0.5)));
		drawtext([Slider.x + 0.5, Slider.y - 0.2], (history_state)_4, color->(0, 0, 0), size->11);
		if(state + 1 <= length(history), drawtext([Slider.x + 0.5, Slider.y - 1.0], (history_(state + 1))_4, color->(0.5, 0.5, 0.5)));
		if(state + 2 <= length(history), drawtext([Slider.x + 0.5, Slider.y - 1.8], (history_(state + 2))_4, color->(0.6, 0.6, 0.6)));
	);
);

init();








;
</script>
<script id="csmousedrag" type="text/x-cindyscript">
//prepare();


pp=mousefilter();
modusdrag(pp)











;text();





;
</script>
<script id="csmouseup" type="text/x-cindyscript">
buttons();
modusreleased(pp);










;init();
text();







;
</script>
<script id="csmousemove" type="text/x-cindyscript">
//pts=allpoints();
//pos=mouse().xy;
//apply(pts,p,if(|p,pos|<pointdist,p.color=(1,1,0),p.color=(1,1,1)));
//repaint();











;
</script>
<script id="csdraw" type="text/x-cindyscript">
draw((-100,screenlimit),(100,screenlimit),color->(.6,.6,.6),size->5);
draw((-100,screenlimit),(100,screenlimit),color->(0.85,0.85,0.85));

draw((screenlimitX,-100),(screenlimitX,100),color->(.6,.6,.6),size->5);
draw((screenlimitX,-100),(screenlimitX,100),color->(0.85,0.85,0.85));

//drawtext([screenlimitX + 1.5, screenlimit - 1.5], "Zum Starten/Reseten des Algorithmus bitte Taste drücken.");

apply(remove(allpoints(), [SliderBegin, SliderEnd, Slider]),p,inspect(p,"pinning",modus==2)); //workaround, gefällt mir noch nicht











;text();







;
</script>
<script id="csmousedown" type="text/x-cindyscript">
// deactivate buttons/modes because slider is pressed
if(dist(mouse().xy, Slider.xy) < 0.1,
  forall(modi, #.pressed = false);
  buttons();
);


pp=mousefilter();
moduspressed(pp);








;text();







;
</script>
    <script type="text/javascript">
var cdy = CindyJS({
  scripts: "cs*",
  defaultAppearance: {
    dimDependent: 0.7,
    fontFamily: "sans-serif",
    lineSize: 1,
    pointSize: 5.0,
    textsize: 12.0
  },
  angleUnit: "°",
  geometry: [
    {name: "SliderEnd", type: "Free", pos: [4.0, 1.696969696969697, -0.24242424242424243], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "SliderBegin", type: "Free", pos: [4.0, -3.272727272727273, -0.24242424242424243], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "SliderLine", type: "Segment", color: [0.502, 0.502, 0.502], args: ["SliderBegin", "SliderEnd"], size: 3},
    {name: "Slider", type: "PointOnSegment", pos: [4.0, -3.272727272727272, -0.24242424242424243], color: [1.0, 1.0, 1.0], args: ["SliderLine"]},
    {name: "J", type: "Free", pos: [-3.4489795918367347, -4.0, -0.5102040816326531], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "J"},
    {name: "I", type: "Free", pos: [4.0, -2.234875444839858, 0.3558718861209964], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "I"},
    {name: "H", type: "Free", pos: [4.0, 2.922693266832918, 0.24937655860349128], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "H"},
    {name: "S14", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["I", "H"], labeled: true, size: 3, printname: " "},
    {name: "G", type: "Free", pos: [-1.9459459459459458, -4.0, 2.7027027027027026], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "G"},
    {name: "S16", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["I", "G"], labeled: true, size: 3, printname: " "},
    {name: "F", type: "Free", pos: [4.0, 0.1764705882352941, 0.7352941176470588], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "F"},
    {name: "S12", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["F", "G"], labeled: true, size: 3, printname: " "},
    {name: "E", type: "Free", pos: [4.0, 1.1583011583011584, 0.3861003861003861], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "E"},
    {name: "S13", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["H", "E"], labeled: true, size: 3, printname: " "},
    {name: "D", type: "Free", pos: [4.0, -1.15, 0.4166666666666667], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "D"},
    {name: "S4", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["E", "D"], labeled: true, size: 3, printname: " "},
    {name: "S6", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["F", "D"], labeled: true, size: 3, printname: " "},
    {name: "S9", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["D", "G"], labeled: true, size: 3, printname: " "},
    {name: "S10", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["H", "D"], labeled: true, size: 3, printname: " "},
    {name: "S15", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["D", "I"], labeled: true, size: 3, printname: " "},
    {name: "C", type: "Free", pos: [4.0, 3.5415019762845854, 0.3952569169960475], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "C"},
    {name: "S20", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "J"], labeled: true, size: 3, printname: " "},
    {name: "S21", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["H", "C"], labeled: true, size: 3, printname: " "},
    {name: "B", type: "Free", pos: [4.0, 2.5306122448979593, 0.6802721088435375], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "B"},
    {name: "S2", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "C"], labeled: true, size: 3, printname: " "},
    {name: "S3", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "D"], labeled: true, size: 3, printname: " "},
    {name: "S5", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "E"], labeled: true, size: 3, printname: " "},
    {name: "S7", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "F"], labeled: true, size: 3, printname: " "},
    {name: "S8", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["G", "B"], labeled: true, size: 3, printname: " "},
    {name: "S11", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "H"], labeled: true, size: 3, printname: " "},
    {name: "S19", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "J"], labeled: true, size: 3, printname: " "},
    {name: "A", type: "Free", pos: [0.10774410774410774, -4.0, -0.33670033670033667], dimDependent: 1, color: [0.0, 0.0, 0.0], labeled: true, size: 9.0, printname: "A"},
    {name: "S1", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "A"], labeled: true, size: 3, printname: " "},
    {name: "S17", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "A"], labeled: true, size: 3, printname: " "},
    {name: "S18", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["J", "A"], labeled: true, size: 3, printname: " "},
    {name: "S22", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["A", "G"], labeled: true, size: 3, printname: " "},
    {name: "S23", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["H", "A"], labeled: true, size: 3, printname: " "},
    {name: "Text1", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Kante", dock: {corner: "LL", offset: [82.0, 13.0]}},
    {name: "Text2", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, minwidth: 57, text: "Entfernen", dock: {corner: "LL", offset: [154.0, 13.0]}},
    {name: "Text0", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Knoten", dock: {corner: "LL", offset: [10.0, 13.0]}},
    {name: "Text3", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, script: "forall(remove(allpoints(), [SliderBegin, SliderEnd, Slider]), removeelement(#)); init()", text: "Alles entfernen", dock: {corner: "LL", offset: [226.0, 13.0]}}
  ],
  ports: [{
    id: "CSCanvas",
    width: 956,
    height: 553,
    transform: [{visibleRect: [-17.38, 14.34, 20.86, -7.78]}],
    background: "rgb(168,176,192)"
  }],
  csconsole: false,
  cinderella: {build: 1901, version: [2, 9, 1901]}
});
    </script>
</head>
<body>
    <div id="CSCanvas"></div>
</body>
</html>