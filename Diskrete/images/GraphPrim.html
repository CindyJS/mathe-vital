<!DOCTYPE html>
<html>
<head>
    <meta charset="UTF-8">
    
    <title>GraphPrim.cdy</title>
    <style type="text/css">
        * {
            margin: 0px;
            padding: 0px;
        }

        #CSConsole {
            background-color: #FAFAFA;
            border-top: 1px solid #333333;
            bottom: 0px;
            height: 200px;
            overflow-y: scroll;
            position: fixed;
            width: 100%;
        }
    </style>
    <link rel="stylesheet" href="https://cindyjs.org/dist/v0.8/CindyJS.css">
    <script type="text/javascript" src="https://cindyjs.org/dist/v0.8/Cindy.js"></script>
<script id="csinit" type="text/x-cindyscript">
// begin environment settings
screenlimitY = 0;
screenlimitX = -13;

pointdist = 0.4;
segdist = 0.4;

pointaddcolor = gray(1);
pointsnapcolor= (1, 1, 0);
pointsize = 7;
segmentcolor = gray(1);
segmentsize = 2;
arrowsize = 7;

directedGraphs = true;
arcBend = 1;
// end environment settings


// begin misc
AL = []; // main data structure (adjacency list + weights + gui information)


allpts() := apply(AL, #_1);
allEdges() := flatten(apply(AL, a, apply(a_2, b, [b_3, if(length(b) == 4, [a_1, b_4, b_1], [a_1, b_1]), b_2])));
allSegs() := apply(flatten(apply(AL, a, a_2)), b, b_3);
allGraphicObjects() := concat(allpts(), apply(flatten(apply(AL, a, a_2)), b, b_3));


inspect(S1B, "pinning", true);
inspect(S1B, "labeled", false);
inspect(S1B, "ptsize", 3);
inspect(S1B, "color", 15);
inspect(S1B, "pointborder", 0);

inspect(S1E, "pinning", true);
inspect(S1E, "labeled", false);
inspect(S1E, "ptsize", 3);
inspect(S1E, "color", 15);
inspect(S1E, "pointborder", 0);

inspect(S1P, "pinning", false);
inspect(S1P, "labeled", false);
inspect(S1P, "ptsize", 5);
inspect(S1P, "color", 0);
inspect(S1P, "pointborder", true);

inspect(sl1, "linesize", 2);
inspect(sl1, "color", 15);
inspect(sl1, "labeled", false);

noPoints = [S1B, S1E, S1P];
noLines = [sl1];
// end misc


// begin buttonhandler
modi = [ButtonVertex, ButtonEdge, ButtonRemove, ButtonRemoveAll];

initmodi() := (
  apply(modi, m, m:"old" = false);
  modus = 1;
);

initmodi();


buttons() := (
  l = select(modi, m, (m:"old") != (m.pressed));

  if(l != [], apply(modi--l, m, m.pressed = false));
  apply(modi, m, m:"old" = m.pressed);

  modus = select(1..length(modi), i, (modi_i).pressed);
  modus = if(modus == [], 0, modus_1);
);
// end buttonhandler


// begin mouseevents
cont(e, pos) := (	// cont checks if position pos is close to edge e
  if(length(e_2) == 3,
    // arc
    mid = meet(perp((e_2_1 + e_2_2)/2, join(e_2_1, e_2_2)), perp((e_2_2 + e_2_3)/2, join(e_2_2, e_2_3)));
    tmp = dist(mid.xy, (e_2_2).xy);

    if(if(area((e_2_1).xy, (e_2_2).xy, (e_2_3).xy) > 0,
         area(mid.xy, (e_2_1).xy, pos) > 0 & area(mid.xy, (e_2_3).xy, pos) < 0, 
	area(mid.xy, (e_2_1).xy, pos) < 0 & area(mid.xy, (e_2_3).xy, pos) > 0
       ) & abs(dist(mid.xy, pos) - tmp) < segdist,
      // true
      pro = mid.xy + tmp*(pos.xy - mid.xy)/dist(mid.xy, pos);
      true;
      ,
      // false
      false;
    );
    ,
    // segment
    tmp = (meet(perp(e_1, pos), e_1)).xy;

    if(area((e_2_1).xy, pos, tmp)*area((e_2_2).xy, pos, tmp) < 0 & |tmp, pos| < segdist,
      // true
      pro = tmp;
      true;
      ,
      // false
      false;
    );
  );
);


closepoints(pos) := select(allpts(), p, |p.xy, pos| < pointdist);
closeedges(pos) := select(allEdges(), e, cont(e, pos));


mousefilter() := (
  pos = mouse().xy;

  if(pos.y < screenlimitY % pos.x < screenlimitX, 
    out = ["none"]
    ,
    cp = closepoints(pos);
    cs = closeedges(pos);

    out = ["new", pos];

    if(length(cs) == 1, out = ["seg", pro, cs_1]);
    if(length(cs) == 2, if(cs_1_1 == cs_2_1, out = ["seg", pro, cs_1, cs_2]));
    if(length(cp) == 1, out = ["old", cp_1]);
  );

  out;
);
// end mouseevents


// begin labeling
stdPointLabels = ["A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z"];
stdPointLabels = concat(stdPointLabels, apply(1..999, "P" + #));

newPointLabel() := (
  pl = 1;
  pts = allpts();

  while(select(pts, p, p.label == stdPointLabels_pl) != [], pl = pl + 1);

  [stdPointLabels_pl, stdPointLabels_pl];
);


stdEdgeLabels = ["a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z"];
stdEdgeLabels = concat(stdEdgeLabels, apply(1..999, "e" + #));

newEdgeLabel() := (
  sl = 1;
  edges = apply(allEdges(), #_1);

  while(select(edges, edges, edges.label == stdEdgeLabels_sl) != [], sl = sl + 1);

  [stdEdgeLabels_sl, " "];
);
// end labeling


addEdge(p1, p2, weight) := (
  // disallow multi-graphs
  if(!contains(apply(select(AL, a, a_1 == p1)_1_2, b, b_1), p2),
    local(e, lab, hp);

    lab = newEdgeLabel();

    // create edge
    if(contains(apply(select(AL, a, a_1 == p2)_1_2, b, b_1), p1),
      // create arc (there is already an segment)
      hp = [p2.y - p1.y, -(p2.x - p1.x)];
      hp = createpoint("HP_" + lab_1, (p2.xy + p1.xy)/2 + arcBend*hp/abs(hp));
      inspect(hp, "visibility", 0);

      e = create([lab_1], "Arc", [p1, hp, p2])_1;
      AL = sort(append(select(AL, #_1 != p1), [p1, append(select(AL, #_1 == p1)_1_2, [p2, weight, e, hp])]), #_1);
      ,
      // create segment
      e = create([lab_1], "Segment", [p1, p2])_1;
      AL = sort(append(select(AL, #_1 != p1), [p1, append(select(AL, #_1 == p1)_1_2, [p2, weight, e])]), #_1);
      
      if(!directedGraphs,
        // add "both" edges
        AL = sort(append(select(AL, #_1 != p2), [p2, append(select(AL, #_1 == p2)_1_2, [p1, weight, e])]), #_1);
      );
    );
    
    // modify new segment
    e.printlabel = lab_2;
    e.color = segmentcolor;
    e.size = segmentsize;
    if(directedGraphs,
      inspect(e, "arrow.mode", 1);
      inspect(e, "arrow.size", arrowsize);
    );

    release(hp, lab, e);
  );
);


generatepoint(pp) := (
  local(p, lab);

  lab = newPointLabel();

  // create point
  p = createpoint(lab_1, pp_2);
  AL = append(AL, [p,[]]);

  // modify new point
  p.printlabel = lab_2;
  p.color = pointaddcolor;
  p.size = pointsize;

  release(lab, p);
);


generatepointonseg(pp) := (
  local(p, s, aa, bb);

  // generate point
  p = generatepoint(pp);

  s = pp_3_1;
  aa = pp_3_2_1;

  if(length(pp_3_2) == 3,
    // arc
    bb = pp_3_2_3;
    ,
    // segment
    bb = pp_3_2_2;
  );

  // create two new segments (splitting up the old one)
  addEdge(aa, p, 1);
  addEdge(p, bb, 1);

  AL = sort(append(select(AL, #_1 != aa), [aa, select(select(AL, a, a_1 == aa)_1_2, b, b_1 != bb)]), #_1);
  if(!directedGraphs,
    // remove "both" directions
    AL = sort(append(select(AL, #_1 != bb), [bb, select(select(AL, a, a_1 == bb)_1_2, b, b_1 != aa)]), #_1);
  );

  // remove after AL adjustment!
  removeelement(s);

  release(bb, aa, s, p);
);
// end geometric constructors


// begin temporary constructors
pointtmp(pp) := (
  if(pp_1 == "new", draw(pp_2, size->pointsize, color->pointaddcolor));
  if(pp_1 == "old", draw(pp_2, size->pointsize, color->pointsnapcolor));
  if(pp_1 == "seg", draw(pp_2, size->pointsize, color->pointsnapcolor));
);

segtmp(pp) := (
  if(pp_1 == "new",
    draw(pp_2, size->pointsize, color->pointaddcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
  if(pp_1 == "old", 
    draw(pp_2, size->pointsize, color->pointsnapcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
  if(pp_1 == "seg",
    draw(pp_2, size->pointsize, color->pointsnapcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
);
// end temporary constructors


// begin
remove(pp) := (
  if(pp_1 == "old",
    AL = sort(apply(select(AL, a, a_1 != pp_2), b, [b_1, select(b_2, c, c_1 != pp_2)]), #_1);
    // remove after AL adjustment!
    removeelement(pp_2);
  );
  if(pp_1 == "seg",
    AL = sort(apply(AL, b, [b_1, select(b_2, c, c_3 != pp_3_1)]), #_1);
    // remove after AL adjustment!
    removeelement(pp_3_1);
  );
);

pointadd(pp):=(
  pout = "";

  if(pp_1 == "new", pout = generatepoint(pp));
  if(pp_1 == "old", pout = pp_2);
  if(pp_1 == "seg", pout = generatepointonseg(pp));

  pout;
);
// end


// begin modi
moduspressed(pp) := (
  if(modus==1, pointtmp(pp));
  if(modus==2, tmppt = pointadd(pp); tmppos = tmppt.xy);
  if(modus==3, );
  if(modus==4, );
);

modusdrag(pp) := (
  if(modus==1, pointtmp(pp));
  if(modus==2, tmppt.xy = tmppos; segtmp(pp));
  if(modus==3, );
  if(modus==4, );
);

modusreleased(pp) := (
  if(modus==1, pointadd(pp));
  if(modus==2, pp2 = pointadd(pp); if(!isundefined(tmppos), addEdge(tmppt, pp2, 1)));
  if(modus==3, remove(pp));
  if(modus==4, );

  if(ButtonWeights.pressed & pp_1 == "seg",
    if(pp_1 == "seg",
      AL = apply(AL, a,
        [a_1, apply(a_2, b,
          if(b_3 == pp_3_1,
            // increase weight
            if(length(b) == 4,
              // arc
              [b_1, if(key() != "-", b_2 + 1, b_2 - 1), b_3, b_4];
              ,
              // segment
              [b_1, if(key() != "-", b_2 + 1, b_2 - 1), b_3]);
            ,
            // do not increase weight
            b;
          );
        )];
      );
    );
  );

  if(ButtonStart.pressed & pp_1 == "old",
    forall(AL, (#_1_1):"start" = false);
    (pp_2):"start" = true;

    repaint();
  );
);
//end modi


updateHelperPoints() := (
  forall(AL, begin,
    forall(begin_2, end,
      if(length(end) == 4,
        // This is an arc ... we have to update helper point!
        hp = [(begin_1).y-(end_1).y,-((begin_1).x-(end_1).x)];
        (end_4).xy = ((begin_1).xy + (end_1).xy)/2 + arcBend*hp/abs(hp);
      );
    );
  );
);


buttons();origin = [5.8, 10];


addPoint(label, xy) := (
  local(ret);

  ret = generatepoint(["", xy + origin]);
  if(label != "", ret.printlabel = label);

  release(ret); 
);


addEdgeByLabels(begin, end, weight):= (
  addEdge(select(allpts(), #.printlabel == begin)_1, select(allpts(), #.printlabel == end)_1, weight);
);


isOldGraph(G) := (
  (length(allpts()) == length(G_1)) &
  ((apply(allpts(), #.printlabel)--(apply(G_1, #_1))) == []) &
  (length(allEdges()) == length(G_2)*2) &
  ((apply(allEdges(), [(#_2_1).printlabel, (#_2_2).printlabel])--(G_2)--apply(G_2, a, [a_2,a_1])) == []);
);


changeGraph(G) := (
  drawCrossings = false;
  clrscr();

  draw((-100, screenlimitY), (100, screenlimitY), color->(.5,.5,.5), size->5);
  draw((-100, screenlimitY), (100, screenlimitY), color->(0.85,0.85, 0.85));

  if(isOldGraph(G),
    deltas = apply(allpts(), p, (select(G_1, g, p.printlabel == g_1)_1)_2 + origin - p.xy);

    repeat(300,
      repeat(length(allpts()), k,
        inspect((allpts()_k), "pinning", false); // workaround
        (allpts()_k).xy = (allpts()_k).xy + (deltas_k)/300;
        updateHelperPoints();
      );
      wait(3);
    );
    ,
    if(AL != [],
      repeat(300,
        forall(allpts(), p,
          inspect(p, "pinning", false); // workaround
          p.xy = p.xy + [37.5/300, 0];
          updateHelperPoints();
        );
        wait(1);
      );
    );

    forall(remove(allpoints(), noPoints), removeelement(#));
    AL = [];
    clrscr();

    forall(G_1, addPoint(#_1, #_2 + [37.5, 0]));
    forall(G_2, addEdgeByLabels(#_1, #_2, #_3));

    repeat(300,
      forall(allpts(), p,
        inspect(p, "pinning", false); // workaround
        p.xy = p.xy - [37.5/300,0];
        updateHelperPoints();
      );
      wait(1);
    );
  );

  drawCrossings = true;
  repaint();
);


changeGraph([
  [
    ["A", gauss(5*exp(2*pi*i*(0/5 + 18/360)))],
    ["B", gauss(5*exp(2*pi*i*(1/5 + 18/360)))],
    ["C", gauss(5*exp(2*pi*i*(2/5 + 18/360)))],
    ["D", gauss(5*exp(2*pi*i*(3/5 + 18/360)))],
    ["E", gauss(5*exp(2*pi*i*(4/5 + 18/360)))],
    ["F", gauss(8.090169942*exp(2*pi*i*(0/5 + 18/360)))],
    ["G", gauss(8.090169942*exp(2*pi*i*(1/5 + 18/360)))],
    ["H", gauss(8.090169942*exp(2*pi*i*(2/5 + 18/360)))],
    ["I", gauss(8.090169942*exp(2*pi*i*(3/5 + 18/360)))],
    ["J", gauss(8.090169942*exp(2*pi*i*(4/5 + 18/360)))]
  ]
  ,
  [
    ["A","C", 4],
    ["B","A", 2],
    ["A","E", 3],
    ["C","E", 4],
    ["B","G", 6],
    ["C","H", 7],
    ["D","I", 8],
    ["E","J", 6],
    ["E","C", 0],
    ["F","G", 5],
    ["G","H", 4],
    ["H","I", 4],
    ["I","J", 3],
    ["F","J", 0],
    ["J","F", 0]
  ]
]);


ButtonWeights.pressed = true;
forall(flatten(apply(AL, a, a_2)), b,
  inspect(b_3, "printname", b_2);
  inspect(b_3, "labeled", true);
);

repaint();// function defintions
prim(root) := (
  F = [];
  X = [root];
  Y = remove(allpts(), root);

  his = append(his, [
    "Starte mit Wurzel " + inspect(X_1, "name") + " - Partitionen: " + X + " und " + Y,
    F,
    X,
    Y,
    []
  ]);

  repeat(length(allpts()) - 1,
    edge = [];

    forall(select(allEdges(), !contains(F, #_1)), e,
      if(edge == [],
        if(contains(X, e_2_1) & contains(Y, e_2_(-1)),
          edge = e;

          his = append(his, [
            "Kantensuche: " + [e_2_1, e_2_(-1)],
            F,
            X,
            Y,
            [edge_1]
          ]);
        );
        ,
        if(contains(X, e_2_1) & contains(Y, e_2_(-1)) & e_3 < edge_3,
          edge = e;

          his = append(his, [
            "optimalere Kante: " + [e_2_1, e_2_(-1)],
            F,
            X,
            Y,
            [edge_1]
          ]);
        );
      );
    );
    
    if(edge != [],
      X = set(concat(X, edge_2_(-1)));
      F = if(egde != [], set(append(F, edge_1)));
      Y = remove(allpts(), X);

      his = append(his, [
        "Partitionen: " + X + " und " + Y,
        F,
        X,
        Y,
        []
      ]);
    );
  );

  [X, F];
);


init() := (
  his = [];
  // HistorySlider.xy = HistorySliderBegin.xy;
  
  if(length(AL) > 0,
    prim(if(length(select(AL, (#_1_1):"start" == true)) > 0, select(AL, (#_1_1):"start" == true)_1_1, AL_1_1));
  );
     
  repaint();
);


text() := (
  forall(allGraphicObjects(), #.color=gray(1));

  state = floor(length(his)*(-S1P.y + S1B.y)/(-S1E.y + S1B.y) + 0.01) + 1;

  if(length(AL) > 0,
    if(state == 0,
      ,
      if(state > length(his), state = length(his));

      forall(his_state_2, #.color = blue(0.8));
      forall(his_state_3, #.color = green(0.6));
      forall(his_state_4, #.color = red(0.8));
      forall(his_state_5, #.color = (0.8, 0.8, 0.2));

      // Slidertext
      if(state - 2 >= 1, drawtext([S1P.x + 0.5, S1P.y + 1.4], (his_(state - 2))_1, color->(0.6, 0.6, 0.6), size->9));
      if(state - 1 >= 1, drawtext([S1P.x + 0.5, S1P.y + 0.6], (his_(state - 1))_1, color->(0.5, 0.5, 0.5), size->10));
      drawtext([S1P.x + 0.5, S1P.y - 0.2], (his)_state_1, color->(0, 0, 0), size->12);
      if(state + 1 <= length(his), drawtext([S1P.x + 0.5, S1P.y - 1.0], (his_(state + 1))_1, color->(0.5, 0.5, 0.5), size->10));
      if(state + 2 <= length(his), drawtext([S1P.x + 0.5, S1P.y - 1.8], (his_(state + 2))_1, color->(0.6, 0.6, 0.6), size->9));
    );
  );
);



// init
root = [];
history = [];

init();
;
</script>
<script id="csmousedrag" type="text/x-cindyscript">
pp=mousefilter();
modusdrag(pp);










































;
</script>
<script id="csmouseup" type="text/x-cindyscript">
buttons();
modusreleased(pp);


// activate/deactivate edge weights
if(ButtonWeights.pressed,
  // show labels
  forall(flatten(apply(AL, a, a_2)), b,
    inspect(b_3, "printname", b_2);
    inspect(b_3, "labeled", true);
  );

  repaint();
  ,
  // disable labels
//  AL = apply(AL, a,
//    [a_1, apply(a_2, b,
//      // reset weight
//      if(length(b) == 4,
//        // arc
//        [b_1, 1, b_3, b_4];
//        ,
//        // segment
//        [b_1, 1, b_3]);
//    )];
//  );

  forall(flatten(apply(AL, a, a_2)), b,
    inspect(b_3, "printname", " ");
    inspect(b_3, "labeled", false);
  );

  repaint();
);


init();










;
</script>
<script id="csmousemove" type="text/x-cindyscript">
//pts=allpts();
//pos=mouse().xy;
//apply(pts, p, if(|p,pos| < pointdist, p.color = pointsnapcolor, p.color = pointaddcolor));
//repaint();
</script>
<script id="csdraw" type="text/x-cindyscript">
draw((-100, screenlimitY), (100, screenlimitY), color->(.5,.5,.5), size->5);
draw((-100, screenlimitY), (100, screenlimitY), color->(0.85,0.85, 0.85));

//draw((screenlimitX, -100), (screenlimitX, 100), color->(.5,.5,.5), size->5);
//draw((screenlimitX, -100), (screenlimitX, 100), color->(0.85,0.85, 0.85));

apply(allpts(), p, inspect(p, "pinning", modus == 2));	// workaround


updateHelperPoints();


if(ButtonColoring.pressed,
  // show coloring helper information
  local(xOffSet, yOffSet);

  xOffSet = 13.8;
  yOffSet = -0.9;

  if(key() == "r", fillcircle([xOffSet - 0.02, yOffSet + 0.18], 0.4, color->red(1), alpha->0.3));
  if(key() == "g", fillcircle([xOffSet + 0.66, yOffSet + 0.18], 0.4, color->green(0.7), alpha->0.3));
  if(key() == "b", fillcircle([xOffSet + 1.35, yOffSet + 0.18], 0.4, color->blue(1), alpha->0.3));
  if(key() == "y", fillcircle([xOffSet + 2.07, yOffSet + 0.18], 0.4, color->(1, 1, 0), alpha->0.3));
  if(key() == "m", fillcircle([xOffSet + 2.755, yOffSet + 0.18], 0.4, color->(1, 0, 1), alpha->0.3));
  if(key() == "w", fillcircle([xOffSet + 3.45, yOffSet + 0.18], 0.4, color->gray(1), alpha->0.3));

  drawtext([xOffSet, yOffSet], "r", color->red(1), align->"center");
  drawtext([xOffSet + 0.7, yOffSet], "g", color->green(0.7), align->"center");
  drawtext([xOffSet + 1.4, yOffSet], "b", color->blue(1), align->"center");
  drawtext([xOffSet + 2.1, yOffSet], "y", color->(1, 1, 0), align->"center");
  drawtext([xOffSet + 2.8, yOffSet], "m", color->(1, 0, 1), align->"center");
  drawtext([xOffSet + 3.5, yOffSet], "w", color->gray(1), align->"center");

  release(yOffSet, xOffSet);
);


if(ButtonWeights.pressed,
  // show wights helper information
  local(xOffSet, yOffSet);

  xOffSet = 23.3;
  yOffSet = -0.9;

  if(key() != "-",
    fillcircle([xOffSet - 0.005, yOffSet + 0.17], 0.4, color->gray(0), alpha->0.3);
    ,
    fillcircle([xOffSet + 0.67, yOffSet + 0.17], 0.4, color->gray(0), alpha->0.3);
  );

  drawtext([xOffSet, yOffSet], "+", color->gray(0), align->"center");
  drawtext([xOffSet + 0.7, yOffSet], "-", color->gray(0), align->"center");

  release(yOffSet, xOffSet);
);





;// set representation G = (V, E)
if(length(AL) > 0,
  VE = "G = (V, E) = ({";
  repeat(length(AL) - 1, VE = VE + (AL_#_1).printlabel + ",");
  VE = VE + (AL_(-1)_1).printlabel + "}, {";

  seg = set(flatten(apply(AL, a, apply(a_2, b, sort([a_1, b_1])))));

  if(length(seg) > 0,
    repeat(length(seg) - 1, VE = VE + if(directedGraphs, "(", "{") + (seg_#_1).printlabel + "," + (seg_#_2).printlabel +
      if(directedGraphs, "),", "},"));
    VE = VE + if(directedGraphs, "(", "{") + (seg_(-1)_1).printlabel + "," + (seg_(-1)_2).printlabel + if(directedGraphs, ")", "}");
  );

  VE = VE + "})";
  ,
  VE = "G = (V, E) = ({}, {})";
);

drawtext([-12.5, 21], VE, size->13);


draw(
  if(length(select(AL, (#_1_1):"start" == true)) > 0, select(AL, (#_1_1):"start" == true)_1_1, AL_1_1),
  color->green(0.6),
  alpha->0.5,
  size->17
);

text();
</script>
<script id="csmousedown" type="text/x-cindyscript">
// deactivate buttons/modes because slider is pressed
if(dist(mouse().xy, S1P.xy) < 0.1,
  forall(modi, #.pressed = false);
  buttons();
);


pp=mousefilter();
moduspressed(pp);




























;
</script>
    <script type="text/javascript">
var cdy = CindyJS({
  scripts: "cs*",
  defaultAppearance: {
    dimDependent: 0.7,
    fontFamily: "sans-serif",
    lineSize: 1,
    pointSize: 5.0,
    textsize: 12.0
  },
  angleUnit: "°",
  geometry: [
    {name: "S1B", type: "Free", pos: [2.4, -4.0, -0.2], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "S1E", type: "Free", pos: [4.0, -0.3333333333333333, -0.3333333333333333], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "sl1", type: "Segment", color: [0.502, 0.502, 0.502], args: ["S1B", "S1E"], size: 2},
    {name: "S1P", type: "PointOnSegment", pos: [2.3999999999999995, -4.0, -0.19999999999999996], color: [1.0, 1.0, 1.0], args: ["sl1"]},
    {name: "ButtonEdge", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Kante", dock: {corner: "LL", offset: [83.0, 13.0]}},
    {name: "ButtonRemove", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, minwidth: 57, text: "entfernen", dock: {corner: "LL", offset: [156.0, 13.0]}},
    {name: "ButtonVertex", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Knoten", dock: {corner: "LL", offset: [10.0, 13.0]}},
    {name: "ButtonRemoveAll", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, script: "forall(remove(allpoints(), noPoints), removeelement(#));\nclrscr();\nAL=[];", text: "alles entfernen", dock: {corner: "LL", offset: [229.0, 13.0]}},
    {name: "Text1", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, visible: false, text: "Gewichte", dock: {corner: "LL", offset: [856.0, 13.0]}},
    {name: "ButtonDirected", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, script: "forall(remove(allpoints(), noPoints), removeelement(#));\nclrscr();\nAL=[];\n\ndirectedGraphs = !directedGraphs;", text: "gerichtete/ungerichtete Graphen", dock: {corner: "LL", offset: [330.0, 13.0]}},
    {name: "Text0", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, visible: false, text: "Färbung", dock: {corner: "LL", offset: [793.0, 13.0]}},
    {name: "ButtonWeights", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, pressed: true, text: "Gewichte", dock: {corner: "LL", offset: [855.0, 13.0]}},
    {name: "ButtonStart", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, text: "Startknoten wählen", dock: {corner: "LL", offset: [731.0, 13.0]}},
    {name: "A", type: "Free", pos: [-3.6570653597404608, -4.0, -0.3464677834545611], dimDependent: 1, color: [0.0, 0.6, 0.0], labeled: true, size: 7.0, printname: "A"},
    {name: "B", type: "Free", pos: [-1.546666666666663, -4.0, -0.26666666666666583], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "B"},
    {name: "C", type: "Free", pos: [-0.36196092833244753, -4.0, -0.34646778345456075], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "C"},
    {name: "D", type: "Free", pos: [-1.9218233845652593, -4.0, -0.671714034727258], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "D"},
    {name: "E", type: "Free", pos: [4.0, 2.725696429954203, 0.4577221366015553], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "E"},
    {name: "F", type: "Free", pos: [4.0, 3.7052931806497766, 0.2964234544648016], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "F"},
    {name: "G", type: "Free", pos: [-1.2824644585641227, -4.0, -0.2211145618213998], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "G"},
    {name: "H", type: "Free", pos: [0.6061468292339861, -4.0, -0.3200000000138392], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "H"},
    {name: "I", type: "Free", pos: [-1.209543401930165, -4.0, -1.1577708759256677], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "I"},
    {name: "J", type: "Free", pos: [4.0, 1.3092648171979748, 0.378957168556491], dimDependent: 1, color: [0.8, 0.0, 0.0], labeled: true, size: 7.0, printname: "J"},
    {name: "a", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["A", "C"], labeled: true, size: 2, printname: "4", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "b", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "A"], labeled: true, size: 2, printname: "2", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "c", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["A", "E"], labeled: true, size: 2, printname: "3", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "d", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "E"], labeled: true, size: 2, printname: "4", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "e", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "G"], labeled: true, size: 2, printname: "6", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "f", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "H"], labeled: true, size: 2, printname: "7", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "g", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["D", "I"], labeled: true, size: 2, printname: "8", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "h", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["E", "J"], labeled: true, size: 2, printname: "6", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "HP_i", type: "Free", pos: [-2.168011987761223, -4.0, -0.5037159753605535], color: [1.0, 0.0, 0.0], alpha: 0.0, labeled: true},
    {name: "i", type: "ArcBy3", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["E", "HP_i", "C"], size: 2, printname: "0", labeled: true, arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "j", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["F", "G"], labeled: true, size: 2, printname: "5", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "k", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["G", "H"], labeled: true, size: 2, printname: "4", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "l", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["H", "I"], labeled: true, size: 2, printname: "4", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "m", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["I", "J"], labeled: true, size: 2, printname: "3", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "n", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["F", "J"], labeled: true, size: 2, printname: "0", arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0},
    {name: "HP_o", type: "Free", pos: [4.0, 2.9932118784556323, 0.3612165674618136], color: [1.0, 0.0, 0.0], alpha: 0.0, labeled: true},
    {name: "o", type: "ArcBy3", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["J", "HP_o", "F"], size: 2, printname: "0", labeled: true, arrowshape: "jet", arrowsides: "==>", arrowsize: 1.4000000000000001, arrowposition: 1.0}
  ],
  ports: [{
    id: "CSCanvas",
    width: 956,
    height: 553,
    transform: [{visibleRect: [-12.774752542372882, 21.68013559322034, 24.402305084745763, 0.17499661016949153]}],
    background: "rgb(168,176,192)"
  }],
  csconsole: false,
  cinderella: {build: 1901, version: [2, 9, 1901]}
});
    </script>
</head>
<body>
    <div id="CSCanvas"></div>
</body>
</html>