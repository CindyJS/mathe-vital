<!DOCTYPE html>
<html>
<head>
    <meta charset="UTF-8">
    
    <title>GraphBFSvsDFS.cdy</title>
    <style type="text/css">
        * {
            margin: 0px;
            padding: 0px;
        }

        #CSConsole {
            background-color: #FAFAFA;
            border-top: 1px solid #333333;
            bottom: 0px;
            height: 200px;
            overflow-y: scroll;
            position: fixed;
            width: 100%;
        }
    </style>
    <link rel="stylesheet" href="https://cindyjs.org/dist/v0.8/CindyJS.css">
    <script type="text/javascript" src="https://cindyjs.org/dist/v0.8/Cindy.js"></script>
<script id="csinit" type="text/x-cindyscript">
// begin environment settings
screenlimitY = 0;
screenlimitX = -13;

pointdist = 0.4;
segdist = 0.4;

pointaddcolor = gray(1);
pointsnapcolor= (1, 1, 0);
pointsize = 7;
segmentcolor = gray(1);
segmentsize = 2;
arrowsize = 7;

directedGraphs = false;
arcBend = 1;
// end environment settings


// begin misc
AL = []; // main data structure (adjacency list + weights + gui information)


allpts() := apply(AL, #_1);
allEdges() := flatten(apply(AL, a, apply(a_2, b, [b_3, if(length(b) == 4, [a_1, b_4, b_1], [a_1, b_1]), b_2])));
allGraphicObjects() := concat(allpts(), apply(flatten(apply(AL, a, a_2)), b, b_3));


inspect(S1B, "pinning", true);
inspect(S1B, "labeled", false);
inspect(S1B, "ptsize", 3);
inspect(S1B, "color", 15);
inspect(S1B, "pointborder", 0);

inspect(S1E, "pinning", true);
inspect(S1E, "labeled", false);
inspect(S1E, "ptsize", 3);
inspect(S1E, "color", 15);
inspect(S1E, "pointborder", 0);

inspect(S1P, "pinning", false);
inspect(S1P, "labeled", true);
inspect(S1P, "ptsize", 5);
inspect(S1P, "color", 0);
inspect(S1P, "pointborder", true);

inspect(sl1, "linesize", 2);
inspect(sl1, "color", 15);
inspect(sl1, "labeled", false);

noPoints = [S1B, S1E, S1P];
noLines = [sl1];
// end misc


// begin buttonhandler
modi = [ButtonVertex, ButtonEdge, ButtonRemove, ButtonDefine];

initmodi() := (
  apply(modi, m, m:"old" = false);
  modus = 1;
);

initmodi();


buttons() := (
  l = select(modi, m, (m:"old") != (m.pressed));

  if(l != [], apply(modi--l, m, m.pressed = false));
  apply(modi, m, m:"old" = m.pressed);

  modus = select(1..length(modi), i, (modi_i).pressed);
  modus = if(modus == [], 0, modus_1);
);
// end buttonhandler


// begin mouseevents
cont(e, pos) := (	// cont checks if position pos is close to edge e
  if(length(e_2) == 3,
    // arc
    mid = meet(perp((e_2_1 + e_2_2)/2, join(e_2_1, e_2_2)), perp((e_2_2 + e_2_3)/2, join(e_2_2, e_2_3)));
    tmp = dist(mid.xy, (e_2_2).xy);

    if(if(area((e_2_1).xy, (e_2_2).xy, (e_2_3).xy) > 0,
         area(mid.xy, (e_2_1).xy, pos) > 0 & area(mid.xy, (e_2_3).xy, pos) < 0, 
	area(mid.xy, (e_2_1).xy, pos) < 0 & area(mid.xy, (e_2_3).xy, pos) > 0
       ) & abs(dist(mid.xy, pos) - tmp) < segdist,
      // true
      pro = mid.xy + tmp*(pos.xy - mid.xy)/dist(mid.xy, pos);
      true;
      ,
      // false
      false;
    );
    ,
    // segment
    tmp = (meet(perp(e_1, pos), e_1)).xy;

    if(area((e_2_1).xy, pos, tmp)*area((e_2_2).xy, pos, tmp) < 0 & |tmp, pos| < segdist,
      // true
      pro = tmp;
      true;
      ,
      // false
      false;
    );
  );
);


closepoints(pos) := select(allpts(), p, |p.xy, pos| < pointdist);
closeedges(pos) := select(allEdges(), e, cont(e, pos));


mousefilter() := (
  pos = mouse().xy;

  if(pos.y < screenlimitY % pos.x < screenlimitX, 
    out = ["none"]
    ,
    cp = closepoints(pos);
    cs = closeedges(pos);

    out = ["new", pos];

    if(length(cs) == 1, out = ["seg", pro, cs_1]);
    if(length(cs) == 2, if(cs_1_1 == cs_2_1, out = ["seg", pro, cs_1, cs_2]));
    if(length(cp) == 1, out = ["old", cp_1]);
  );

  out;
);
// end mouseevents


// begin labeling
stdPointLabels = ["A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z"];
stdPointLabels = concat(stdPointLabels, apply(1..999, "P" + #));

newPointLabel() := (
  pl = 1;
  pts = allpts();

  while(select(pts, p, p.label == stdPointLabels_pl) != [], pl = pl + 1);

  [stdPointLabels_pl, stdPointLabels_pl];
);


stdEdgeLabels = ["a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z"];
stdEdgeLabels = concat(stdEdgeLabels, apply(1..999, "e" + #));

newEdgeLabel() := (
  sl = 1;
  edges = apply(allEdges(), #_1);

  while(select(edges, edges, edges.label == stdEdgeLabels_sl) != [], sl = sl + 1);

  [stdEdgeLabels_sl, " "];
);
// end labeling


addEdge(p1, p2) := (
  // disallow multi-graphs
  if(!contains(apply(select(AL, a, a_1 == p1)_1_2, b, b_1), p2),
    local(e, lab, hp);

    lab = newEdgeLabel();

    // create edge
    if(contains(apply(select(AL,a,a_1==p2)_1_2,b,b_1),p1),
      // create arc (there is already an segment)
      hp = [p2.y - p1.y, -(p2.x - p1.x)];
      hp = createpoint("HP_" + lab_1, (p2.xy + p1.xy)/2 + arcBend*hp/abs(hp));
      inspect(hp, "visibility", 0);

      e = create([lab_1], "Arc", [p1, hp, p2])_1;
      AL = sort(append(select(AL, #_1 != p1), [p1, append(select(AL, #_1 == p1)_1_2, [p2, 1, e, hp])]), #_1);
      ,
      // create segment
      e = create([lab_1], "Segment", [p1, p2])_1;
      AL = sort(append(select(AL, #_1 != p1), [p1, append(select(AL, #_1 == p1)_1_2, [p2, 1, e])]), #_1);
      
      if(!directedGraphs,
        // add "both" edges
        AL = sort(append(select(AL, #_1 != p2), [p2, append(select(AL, #_1 == p2)_1_2, [p1, 1, e])]), #_1);
      );
    );
    
    // modify new segment
    e.printlabel = lab_2;
    e.color = segmentcolor;
    e.size = segmentsize;
    if(directedGraphs,
      inspect(e, "arrow.mode", 1);
      inspect(e, "arrow.size", arrowsize);
    );

    release(hp, lab, e);
  );
);


generatepoint(pp) := (
  local(p, lab);

  lab = newPointLabel();

  // create point
  p = createpoint(lab_1, pp_2);
  AL = append(AL, [p,[]]);

  // modify new point
  p.printlabel = lab_2;
  p.color = pointaddcolor;
  p.size = pointsize;

  release(lab, p);
);


generatepointonseg(pp) := (
  local(p, s, aa, bb);

  // generate point
  p = generatepoint(pp);

  s = pp_3_1;
  aa = pp_3_2_1;

  if(length(pp_3_2) == 3,
    // arc
    bb = pp_3_2_3;
    ,
    // segment
    bb = pp_3_2_2;
  );

  // create two new segments (splitting up the old one)
  addEdge(aa, p);
  addEdge(p, bb);

  AL = sort(append(select(AL, #_1 != aa), [aa, select(select(AL, a, a_1 == aa)_1_2, b, b_1 != bb)]), #_1);
  if(!directedGraphs,
    // remove "both" directions
    AL = sort(append(select(AL, #_1 != bb), [bb, select(select(AL, a, a_1 == bb)_1_2, b, b_1 != aa)]), #_1);
  );

  // remove after AL adjustment!
  removeelement(s);

  release(bb, aa, s, p);
);
// end geometric constructors


// begin temporary constructors
pointtmp(pp) := (
  if(pp_1 == "new", draw(pp_2, size->pointsize, color->pointaddcolor));
  if(pp_1 == "old", draw(pp_2, size->pointsize, color->pointsnapcolor));
  if(pp_1 == "seg", draw(pp_2, size->pointsize, color->pointsnapcolor));
);

segtmp(pp) := (
  if(pp_1 == "new",
    draw(pp_2, size->pointsize, color->pointaddcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
  if(pp_1 == "old", 
    draw(pp_2, size->pointsize, color->pointsnapcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
  if(pp_1 == "seg",
    draw(pp_2, size->pointsize, color->pointsnapcolor);
    draw([tmppt, pp_2], color->segmentcolor, linesize->(segmentsize + 1));
  );
);
// end temporary constructors


// begin
remove(pp) := (
  if(pp_1 == "old",
    AL = sort(apply(select(AL, a, a_1 != pp_2), b, [b_1, select(b_2, c, c_1 != pp_2)]), #_1);
    // remove after AL adjustment!
    removeelement(pp_2);
  );
  if(pp_1 == "seg",
    AL = sort(apply(AL, b, [b_1, select(b_2, c, c_3 != pp_3_1)]), #_1);
    // remove after AL adjustment!
    removeelement(pp_3_1);
  );
);

pointadd(pp):=(
  pout = "";

  if(pp_1 == "new", pout = generatepoint(pp));
  if(pp_1 == "old", pout = pp_2);
  if(pp_1 == "seg", pout = generatepointonseg(pp));

  pout;
);
// end


// begin modi
moduspressed(pp) := (
  if(modus==1, pointtmp(pp));
  if(modus==2, tmppt = pointadd(pp); tmppos = tmppt.xy);
  if(modus==3, );
  if(modus==4, );
);

modusdrag(pp) := (
  if(modus==1, pointtmp(pp));
  if(modus==2, tmppt.xy = tmppos; segtmp(pp));
  if(modus==3, );
  if(modus==4, );
);

modusreleased(pp) := (
  if(modus==1, pointadd(pp));
  if(modus==2, pp2 = pointadd(pp); if(!isundefined(tmppos), addEdge(tmppt, pp2)));
  if(modus==3, remove(pp));
  if(modus==4,
    if(pp_1 = "old",
      if(startFlag,
        (select(AL, (#_1_1):"ss" == "start")_1_1):"ss" = "none";
        (pp_2):"ss" = "start";
        startFlag = !startFlag;
        ,
        (select(AL, (#_1_1):"ss" == "stop")_1_1):"ss" = "none";
        (pp_2):"ss" = "stop";
        startFlag = !startFlag;
      );
      repaint();
    );
  );
);
//end modi


buttons();




;preDefineGraph() := (
  // vertices (with embedding)
  A = generatepoint(["", [7, 19]]);
  B = generatepoint(["", [-1, 15]]);
  C = generatepoint(["", [15, 15]]);
  D = generatepoint(["", [-5, 11]]);
  E = generatepoint(["", [3, 11]]);
  F = generatepoint(["", [11, 11]]);
  G = generatepoint(["", [19, 11]]);
  H = generatepoint(["", [-3, 7]]);
  I = generatepoint(["", [4, 7]]);
  J = generatepoint(["", [17, 7]]);
  K = generatepoint(["", [21, 7]]);
  L = generatepoint(["", [2, 3]]);
  M = generatepoint(["", [6, 3]]);

  // edges
  addEdge(A, B);
  addEdge(A, C);

  addEdge(B, D);
  addEdge(B, E);

  addEdge(C, F);
  addEdge(C, G);

  addEdge(D, H);

  addEdge(E, I);

  addEdge(I, L);
  addEdge(I, M);

  addEdge(C, F);
  addEdge(C, G);

  addEdge(G, J);
  addEdge(G, K);
);


preDefineGraph();








;startFlag = true;


breadthFirstSearch(start, stop) := (
  local(queue, marked, usedEdges, found);

  queue = [start];
  marked = [];
  usedEdges = [];
  found = false;

  his = append(his,
    [
      [],
      marked,
      [],
      "Beginne Breitensuche im Knoten " + start.printlabel + "."
    ]
  );
  his = append(his,
    [
      queue,
      marked,
      [],
      "F체ge " + start.printlabel + " zur Warteschlange (Queue, rote Knoten)."
    ]
  );

  while(!found & length(queue) > 0,
    if(queue_1 == stop, 
      found = true;

      his = append(his,
        [
          remove(queue, [queue_1]),
          append(marked, queue_1),
          usedEdges,
          "Stopknoten " + stop.printlabel + " erreicht."
        ]
      );
      ,
      marked = append(marked, queue_1);

      his = append(his,
        [
          queue,
          marked,
          usedEdges = concat(usedEdges, apply(select(AL, a, queue_1 == a_1)_1_2, b, b_3)),
          "Entferne " + (queue_1).printlabel + " aus Queue & Markiere "  + (queue_1).printlabel + " als besucht (gr체n)."
        ]
      );
      his = append(his,
        [
          concat(remove(queue, [queue_1]), remove(apply(select(AL, a, queue_1 == a_1)_1_2, b, b_1), marked)),
          marked,
          usedEdges = concat(usedEdges, apply(select(AL, a, queue_1 == a_1)_1_2, b, b_3)),
          "H채nge unmarkierte Nachbarn von " + (queue_1).printlabel + " in die Queue (rote Knoten)."
        ]
      );

      queue = concat(remove(queue, [queue_1]), remove(apply(select(AL, a, queue_1 == a_1)_1_2, b, b_1), marked));
    );
  );

  release(queue, marked, found);
);


depthFirstSearch(start, stop) := (
  his = append(his,
    [
      [start],
      remove(marked, [start]),
      usedEdges,
      "Rekursionsbeginn im Knoten " + start.printlabel + "."
    ]
  );

  marked = append(marked, start);
  local(k, stack, found);
  found = false;

  if(start == stop,
    found = true;
    his = append(his,
      [
        [start],
        remove(marked, [start]),
        usedEdges,
        "Rekursionsabbruch im Knoten " + start.printlabel + " - Knoten gefunden!"
      ]
    );
    ,
    stack = apply(select(AL, a, start == a_1)_1_2, b, [b_1, b_3]);
    k = 1;

    while(!found & k <= length(stack),
      if(!contains(marked, stack_k_1),
        usedEdges = append(usedEdges, stack_k_2);

        his = append(his,
          [
            [start],
            remove(marked, [start]),
            usedEdges,
            "Starte Tiefensuche auf Knoten " + (stack_k_1).printlabel + "."
          ]
        );

        found = depthFirstSearch(stack_k_1, stop);
      );
      k = k + 1;
    );

    if(found,
      his = append(his,
        [
          [start],
          remove(marked, [start]),
          usedEdges,
          "Rekursionsabbruch im Knoten " + start.printlabel + " - Knoten gefunden!"
        ]
      );
      ,
      his = append(his,
        [
          [start],
          remove(marked, [start]),
          usedEdges,
          "Rekursionsabbruch im Knoten " + start.printlabel + " - Knoten nicht gefunden!"
        ]
      );
    )

  );

  release(k, stack, found);
);


init() := (
  his = [];

if(length(AL) > 2,
  if(isundefined(select(AL, (#_1_1):"ss" == "start")_1_1), 
    start = AL_1_1;
    ,
    start = select(AL, (#_1_1):"ss" == "start")_1_1;
  );

  if(isundefined(select(AL, (#_1_1):"ss" == "stop")_1_1), 
    stop = AL_length(AL)_1;
    ,
    stop = select(AL, (#_1_1):"ss" == "stop")_1_1;
  );
);

  if(isundefined(bfs), bfs = true);

  if(bfs & !isundefined(start) & !isundefined(stop),
    breadthFirstSearch(start, stop);
    ,
    marked = [];
    usedEdges = [];
    stack = [];
    depthFirstSearch(start, stop);
  );
);


text() := (
  forall(allGraphicObjects(), #.color=gray(1));

  state = floor(length(his)*(-S1P.y + S1B.y)/(-S1E.y + S1B.y) + 0.01) + 1;

  if(state == 0,
    ,
    if(state > length(his), state = length(his));

    forall(his_state_1, #.color = red(0.8));
    forall(his_state_2, #.color = green(0.8));
    forall(his_state_3, #.color = blue(0.8));

    // Slidertext
    if(state - 2 >= 1, drawtext([S1P.x + 0.5, S1P.y + 1.4], (his_(state - 2))_4, color->(0.6, 0.6, 0.6), size->9));
    if(state - 1 >= 1, drawtext([S1P.x + 0.5, S1P.y + 0.6], (his_(state - 1))_4, color->(0.5, 0.5, 0.5), size->10));
    drawtext([S1P.x + 0.5, S1P.y - 0.2], (his)_state_4, color->(0, 0, 0), size->12);
    if(state + 1 <= length(his), drawtext([S1P.x + 0.5, S1P.y - 1.0], (his_(state + 1))_4, color->(0.5, 0.5, 0.5), size->10));
    if(state + 2 <= length(his), drawtext([S1P.x + 0.5, S1P.y - 1.8], (his_(state + 2))_4, color->(0.6, 0.6, 0.6), size->9));
  );
);


init();
</script>
<script id="csmousedrag" type="text/x-cindyscript">
pp=mousefilter();
modusdrag(pp);








































;
</script>
<script id="csmouseup" type="text/x-cindyscript">
buttons();
modusreleased(pp);


// activate/deactivate edge weights
if(ButtonWeights.pressed,
  // show labels
  forall(flatten(apply(AL, a, a_2)), b,
    inspect(b_3, "printname", b_2);
    inspect(b_3, "labeled", true);
  );

  repaint();
  ,
  // disable labels
  AL = apply(AL, a,
    [a_1, apply(a_2, b,
      // reset weight
      if(length(b) == 4,
        // arc
        [b_1, 1, b_3, b_4];
        ,
        // segment
        [b_1, 1, b_3]);
    )];
  );

  forall(flatten(apply(AL, a, a_2)), b,
    inspect(b_3, "printname", " ");
    inspect(b_3, "labeled", false);
  );

  repaint();
);


// activate/deactivate verticies coloring
if(ButtonColoring.pressed,
  // show coloring
  ,
  // disable coloring
  forall(AL, a, (a_1).color = pointaddcolor);
);


init();













;
</script>
<script id="csmousemove" type="text/x-cindyscript">
if(!ButtonColoring.pressed,
  // only of coloring mode is disabled
  pts=allpts();
  pos=mouse().xy;
  apply(pts, p, if(|p,pos| < pointdist, p.color = pointsnapcolor, p.color = pointaddcolor));
  repaint();
);







;
</script>
<script id="csdraw" type="text/x-cindyscript">
draw((-100, screenlimitY), (100, screenlimitY), color->(.5,.5,.5), size->5);
draw((-100, screenlimitY), (100, screenlimitY), color->(0.85,0.85, 0.85));

//draw((screenlimitX, -100), (screenlimitX, 100), color->(.5,.5,.5), size->5);
//draw((screenlimitX, -100), (screenlimitX, 100), color->(0.85,0.85, 0.85));

apply(allpts(), p, inspect(p, "pinning", modus == 2));	// workaround


forall(AL, begin,
  forall(begin_2, end,
    if(length(end) == 4,
      // This is an arc ... we have to update helper point!
      hp = [(begin_1).y-(end_1).y,-((begin_1).x-(end_1).x)];
      (end_4).xy = ((begin_1).xy + (end_1).xy)/2 + arcBend*hp/abs(hp);
    );
  );
);// set representation G = (V, E)
if(length(AL) > 0,
  VE = "G = (V, E) = ({";
  repeat(length(AL) - 1, VE = VE + (AL_#_1).label + ",");
  VE = VE + (AL_(-1)_1).label + "}, {";

  seg = set(flatten(apply(AL, a, apply(a_2, b, sort([a_1, b_1])))));

  if(length(seg) > 0,
    repeat(length(seg) - 1, VE = VE + if(directedGraphs, "(", "{") + (seg_#_1).label + "," + (seg_#_2).label +
      if(directedGraphs, "),", "},"));
    VE = VE + if(directedGraphs, "(", "{") + (seg_(-1)_1).label + "," + (seg_(-1)_2).label + if(directedGraphs, ")", "}");
  );

  VE = VE + "})";
  ,
  VE = "G = (V, E) = ({}, {})";
);

drawtext([-12.5, 21], VE, size->13);


if(ButtonDefine.pressed,
  drawtext(
    [21.1, 0.3],
    if(startFlag, "Startknoten anclicken!", "Stopknoten anclicken!"),
    color->if(startFlag, green(0.6), red(0.8)),
    align->"center"
  );
);


drawtext([5.8, 0.3], "Modus: " + if(bfs, "Breitensuche", "Tiefensuche") + " (Start: " + start.printlabel + "/ Stop: " + stop.printlabel + ")", align->"center", size->18, color->red(0.8));


if(!isundefined(start), draw(start, size->18, color->green(0.8), alpha->0.5));
if(!isundefined(stop), draw(stop, size->18, color->red(0.8), alpha->0.5));


text();
</script>
<script id="csmousedown" type="text/x-cindyscript">
pp=mousefilter();
moduspressed(pp);




















;
</script>
    <script type="text/javascript">
var cdy = CindyJS({
  scripts: "cs*",
  defaultAppearance: {
    dimDependent: 0.7,
    fontFamily: "sans-serif",
    lineSize: 1,
    pointSize: 5.0,
    textsize: 12.0
  },
  angleUnit: "째",
  geometry: [
    {name: "S1B", type: "Free", pos: [2.4, -4.0, -0.2], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "S1E", type: "Free", pos: [4.0, -0.3333333333333333, -0.3333333333333333], color: [0.502, 0.502, 0.502], pinned: true, size: 3.0, border: false},
    {name: "sl1", type: "Segment", color: [0.502, 0.502, 0.502], args: ["S1B", "S1E"], size: 2},
    {name: "S1P", type: "PointOnSegment", pos: [2.3999999999999995, -4.0, -0.2], color: [1.0, 1.0, 1.0], args: ["sl1"], labeled: true, printname: " "},
    {name: "A", type: "Free", pos: [-1.4736842105263157, -4.0, -0.21052631578947367], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "A"},
    {name: "B", type: "Free", pos: [0.26666666666666666, -4.0, -0.26666666666666666], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "B"},
    {name: "C", type: "Free", pos: [4.0, 4.0, 0.26666666666666666], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "C"},
    {name: "D", type: "Free", pos: [1.8181818181818181, -4.0, -0.36363636363636365], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "D"},
    {name: "E", type: "Free", pos: [-1.0909090909090908, -4.0, -0.36363636363636365], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "E"},
    {name: "F", type: "Free", pos: [4.0, 4.0, 0.36363636363636365], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "F"},
    {name: "G", type: "Free", pos: [4.0, 2.3157894736842106, 0.21052631578947367], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "G"},
    {name: "H", type: "Free", pos: [1.7142857142857142, -4.0, -0.5714285714285714], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "H"},
    {name: "I", type: "Free", pos: [-2.2857142857142856, -4.0, -0.5714285714285714], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "I"},
    {name: "J", type: "Free", pos: [4.0, 1.6470588235294117, 0.23529411764705882], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "J"},
    {name: "K", type: "Free", pos: [4.0, 1.3333333333333333, 0.19047619047619047], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "K"},
    {name: "L", type: "Free", pos: [-2.6666666666666665, -4.0, -1.3333333333333333], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "L"},
    {name: "M", type: "Free", pos: [4.0, 2.0, 0.6666666666666666], dimDependent: 1, color: [1.0, 1.0, 1.0], labeled: true, size: 7.0, printname: "M"},
    {name: "a", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["A", "B"], labeled: true, size: 2, printname: " "},
    {name: "b", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["A", "C"], labeled: true, size: 2, printname: " "},
    {name: "c", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "D"], labeled: true, size: 2, printname: " "},
    {name: "d", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["B", "E"], labeled: true, size: 2, printname: " "},
    {name: "e", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "F"], labeled: true, size: 2, printname: " "},
    {name: "f", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["C", "G"], labeled: true, size: 2, printname: " "},
    {name: "g", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["D", "H"], labeled: true, size: 2, printname: " "},
    {name: "h", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["E", "I"], labeled: true, size: 2, printname: " "},
    {name: "i", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["I", "L"], labeled: true, size: 2, printname: " "},
    {name: "j", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["I", "M"], labeled: true, size: 2, printname: " "},
    {name: "k", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["G", "J"], labeled: true, size: 2, printname: " "},
    {name: "l", type: "Segment", dimDependent: 1, color: [1.0, 1.0, 1.0], args: ["G", "K"], labeled: true, size: 2, printname: " "},
    {name: "ButtonEdge", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Kante", dock: {corner: "LL", offset: [83.0, 13.0]}},
    {name: "ButtonRemove", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, minwidth: 57, text: "entfernen", dock: {corner: "LL", offset: [156.0, 13.0]}},
    {name: "ButtonVertex", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.012, 0.655, 0.737], fillalpha: 0.27272728085517883, minwidth: 57, text: "Knoten", dock: {corner: "LL", offset: [10.0, 13.0]}},
    {name: "ButtonRemoveAll", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.757, 0.0, 0.0], fillalpha: 0.27272728085517883, script: "forall(remove(allpoints(), noPoints), removeelement(#));\nclrscr();\nAL=[];", text: "alles entfernen", dock: {corner: "LL", offset: [229.0, 13.0]}},
    {name: "ButtonDirected", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, script: "forall(remove(allpoints(), noPoints), removeelement(#));\nclrscr();\nAL=[];\n\ndirectedGraphs = !directedGraphs;", text: "gerichtete/ungerichtete Graphen", dock: {corner: "LL", offset: [330.0, 13.0]}},
    {name: "ButtonSearch", type: "Button", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, script: "bfs = !bfs;", text: "Breitensuche/Tiefensuche", dock: {corner: "LL", offset: [532.0, 13.0]}},
    {name: "ButtonDefine", type: "ToggleButton", color: [0.0, 0.0, 0.0], fillcolor: [0.98, 1.0, 0.62], fillalpha: 0.27272728085517883, text: "definiere Start-/Stopknoten", dock: {corner: "LL", offset: [790.0, 13.0]}}
  ],
  ports: [{
    id: "CSCanvas",
    width: 956,
    height: 553,
    transform: [{visibleRect: [-12.774752542372882, 21.68013559322034, 24.402305084745763, 0.17499661016949153]}],
    background: "rgb(168,176,192)"
  }],
  csconsole: false,
  cinderella: {build: 1901, version: [2, 9, 1901]}
});
    </script>
</head>
<body>
    <div id="CSCanvas"></div>
</body>
</html>